plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'com.braintreepayments.api.popupbridge'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        compileSdk rootProject.compileSdkVersion

        consumerProguardFiles 'proguard.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions.unitTests.includeAndroidResources = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation libs.browser.switch
    implementation libs.appcompat
    implementation libs.lifecycle.runtime

    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.robolectric
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.kotlin.test
}

// region signing and publishing

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.braintreepayments.api'
                artifactId = 'popup-bridge'
                version = rootProject.versionName

                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'popup-bridge'
                    packaging = 'aar'
                    description = 'PopupBridge is an Android library that allows WebViews to open popup windows in a browser and send data back to the WebView.'
                    url = 'https://github.com/braintree/popup-bridge-android'

                    scm {
                        url = 'scm:git@github.com:braintree/popup-bridge-android.git'
                        connection = 'scm:git@github.com:braintree/popup-bridge-android.git'
                        developerConnection = 'scm:git@github.com:braintree/popup-bridge-android.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}


// endregion